apply plugin: 'com.android.library'

configurations {
    upload
}

group = groupId
version = buildNumber == 'SNAPSHOT' ? "${baseVersion}-${buildNumber}" : "${baseVersion}.${buildNumber}"

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName version
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // We use some classes from the deprecated Apache HTTP Client library
    useLibrary 'org.apache.http.legacy'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.mcxiaoke.volley:library:1.0.18'
    compile 'com.parse.bolts:bolts-android:1.1.3'

    // Useful annotations.
    compile 'org.jetbrains:annotations:13.0'

    // Mocks for testing.
    androidTestCompile "org.mockito:mockito-core:1.10.19"
    androidTestCompile "com.crittercism.dexmaker:dexmaker:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:1.4"
}

apply from: file("repository.gradle")
def fullDocumentationDir(variant) {
    return "${buildDir.getPath()}/docs/java-docs-$variant.name/";
}

task createTestPropertyFile << {
    def propertyFile = new File("$projectDir/src/androidTest/assets/test.properties")
    if (!propertyFile.exists()) propertyFile.createNewFile()
    def props = new Properties()

    if (project.hasProperty('testClientId')) props['testClientId'] = testClientId
    if (project.hasProperty('testClientSecret')) props['testClientSecret'] = testClientSecret
    if (project.hasProperty('testApiUri')) props['testApiUri'] = testApiUri
    if (project.hasProperty('testUserCenterUri')) props['testUserCenterUri'] = testUserCenterUri

    propertyFile.withWriter("utf-8") {
        props.store(it, "test properties")
    }
}

task createIntegrationGuide(type: Exec) {
    commandLine './createIntegrationGuide.sh', "${project.projectDir}/src/doc", project.buildDir
}

tasks.whenTaskAdded { task ->
    if (task.name.equals("assembleDebug")) {
        task.dependsOn.add(createTestPropertyFile)
    }
}

android.libraryVariants.all { variant ->
    task("jar${variant.name.capitalize()}", type: Jar) {
        dependsOn variant.javaCompile
        appendix = variant.name
        from variant.javaCompile.destinationDir
    }
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        destinationDir = file(fullDocumentationDir(variant))
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
	    options.encoding = 'UTF-8'
    }
}

artifacts {
    upload file('build/outputs/aar/ginisdk-release.aar')
}

android.libraryVariants.all { variant ->
    task("build${variant.name.capitalize()}Zip", type: Zip) {
        description "Generates the zip with jar, license and docs for $variant.name"
        dependsOn "jar${variant.name.capitalize()}"
        dependsOn "generate${variant.name.capitalize()}Javadoc"

        baseName "ginisdk"
        version android.defaultConfig.versionName
        classifier variant.name
        from files(buildDir, buildDir.getPath() + '/libs')
        include("ginisdk-${variant.name}.jar")
        include 'LICENSE'
        rename("ginisdk-${variant.name}.jar", "ginisdk-${android.defaultConfig.versionName}.jar");

        from (fullDocumentationDir(variant)) {
            into "doc-" + android.defaultConfig.versionName;
            include "**/*.html"
            include "**/*.css"
            include "**/*.js"
        }

        from rootDir
        include 'LICENSE'
    }
}

apply from: rootProject.file('gradle/javadoc_coverage.gradle')
