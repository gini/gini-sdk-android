apply plugin: 'com.android.library'

configurations {
    upload
}

group = groupId
version = "${baseVersion}"

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName version

        // Use the test runner with JUnit4 support
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.volley:volley:1.2.0'
    api 'com.parse.bolts:bolts-android:1.4.0'
    implementation 'com.datatheorem.android.trustkit:trustkit:1.1.3'
    implementation 'androidx.core:core:1.3.2'
    implementation 'androidx.preference:preference:1.1.1'

    // Mocks for testing.
    androidTestImplementation "org.mockito:mockito-core:1.10.19"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker:1.4"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-dx:1.4"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-mockito:1.4"
    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation "com.android.support.test:rules:1.0.2"
}

apply from: file("repository.gradle")

def fullDocumentationDir(variant) {
    return "${buildDir.getPath()}/docs/java-docs-$variant.name/";
}

def getLocalProperties = {
    File propertiesFile = file('local.properties')
    if (propertiesFile.exists()) {
        Properties properties = new Properties()
        propertiesFile.withInputStream { instr ->
            properties.load(instr)
        }
        return properties
    }
}

def setProperty(key, props, localProps) {
    if (project.hasProperty(key)) {
        props[key] = project.property(key)
    } else {
        props[key] = localProps?.get(key) ?: ''
    }
}

task createTestPropertyFile {
    doLast {
        def propertyFile = new File("$projectDir/src/androidTest/assets/test.properties")
        if (!propertyFile.exists()) propertyFile.createNewFile()
        def props = new Properties()

        def localProperties = getLocalProperties()

        setProperty('testClientId', props, localProperties)
        setProperty('testClientSecret', props, localProperties)
        setProperty('testClientIdAccounting', props, localProperties)
        setProperty('testClientSecretAccounting', props, localProperties)
        setProperty('testApiUri', props, localProperties)
        setProperty('testApiUriAccounting', props, localProperties)
        setProperty('testUserCenterUri', props, localProperties)

        propertyFile.withWriter("utf-8") {
            props.store(it, "test properties")
        }
    }
}

task createIntegrationGuide(type: Exec) {
    commandLine './createIntegrationGuide.sh', "${project.projectDir}/src/doc", project.buildDir
}

tasks.whenTaskAdded { task ->
    if (task.name.endsWith("Test")) {
        task.dependsOn.add(createTestPropertyFile)
    }
}

android.libraryVariants.all { variant ->
    task("jar${variant.name.capitalize()}", type: Jar) {
        dependsOn variant.javaCompile
        appendix = variant.name
        from variant.javaCompile.destinationDir
    }
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        destinationDir = file(fullDocumentationDir(variant))
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
	    options.encoding = 'UTF-8'
    }
}

artifacts {
    upload file('build/outputs/aar/ginisdk-release.aar')
}

android.libraryVariants.all { variant ->
    task("build${variant.name.capitalize()}Zip", type: Zip) {
        description "Generates the zip with jar, license and docs for $variant.name"
        dependsOn "jar${variant.name.capitalize()}"
        dependsOn "generate${variant.name.capitalize()}Javadoc"

        baseName "ginisdk"
        version android.defaultConfig.versionName
        classifier variant.name
        from files(buildDir, buildDir.getPath() + '/libs')
        include("ginisdk-${variant.name}.jar")
        include 'LICENSE'
        rename("ginisdk-${variant.name}.jar", "ginisdk-${android.defaultConfig.versionName}.jar")

        from(fullDocumentationDir(variant)) {
            into "doc-" + android.defaultConfig.versionName
            include "**/*.html"
            include "**/*.css"
            include "**/*.js"
        }

        from rootDir
        include 'LICENSE'
    }
}

apply from: rootProject.file('gradle/javadoc_coverage.gradle')
